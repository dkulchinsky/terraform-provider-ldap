---
page_title: "Provider: ldap"
description: |-
---

# LDAP Provider

## Data Sources

The provider makes it possible to read all details about a single LDAP entry or about a set of LDAP entries.

This is done in a strict generic way: the details are accessible through the attribute `data_json`.

Attributes can be ignored by `ignore_attributes` or `ignore_attribute_patterns`.

Attributes can be encoded base64 by `base64encode_attributes` or `base64encode_attribute_patterns`.

Since version v1.4 `dn` can be uses as alternative to `ou` and `filter`.

## Resources

The provider makes it possible to provide an LDAP entry. This can be used to create, modify, and delete LDAP entries.

This is done in a strict generic way: the details are specified through the attribute `data_json`.

## Example Usage

```terraform
terraform {
  required_providers {
    ldap = {
      source  = "l-with/ldap"
      version = ">= 0.4"
    }
  }
}

variable "ldap_bind_user" {}
variable "ldap_bind_password" {}

provider "ldap" {
  host = "example.com"
  port = 636
  tls  = true

  bind_user     = var.ldap_bind_user
  bind_password = var.ldap_bind_password
}

data "ldap_entry" "user" {
  ou     = "ou=People,dc=example,dc=com"
  filter = "mail=user@example.com"
}

resource "ldap_entry" "users_example_com" {
  dn = "ou=users,dc=example,dc=com"
  data_json = jsonencode({
    objectClass = ["organizationalUnit"]
  })
}

resource "ldap_entry" "user_jim_mit" {
  dn = "uid=jimmit01,${ldap_entry.users_example_com.dn}"
  data_json = jsonencode({
    objectClass = ["inetOrgPerson"]
    ou          = ["users"]
    givenName   = ["Jim"]
    sn          = ["Mit"]
    cn          = ["Jim Mit"]
  })
}

data "ldap_entry" "user_jim_mit" {
  depends_on = [ldap_entry.user_jim_mit]

  dn = "uid=jimmit01,${ldap_entry.users_example_com.dn}"
}

locals {
  user_data         = jsondecode(data.ldap_entry.user.data_json)
  user_jim_mit_data = jsondecode(data.ldap_entry.user_jim_mit.data_json)
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `bind_password` (String) LDAP password, can optionally be passed as `LDAP_BIND_PASSWORD`environment variable
- `bind_user` (String) LDAP username, can optionally be passed as `LDAP_BIND_USER`environment variable
- `host` (String) LDAP host, can optionally be passed as `LDAP_HOST`environment variable
- `port` (Number) LDAP port, can optionally be passed as `LDAP_PORT`environment variable
- `tls` (Boolean) Enable the TLS encryption for LDAP (LDAPS). Default, is `false`.
- `tls_insecure` (Boolean) Don't verify the server TLS certificate. Default is `false`.