---
page_title: "Provider: ldap"
description: |-
---

# LDAP Provider

## Data Sources

The provider makes it possible to read all details about a single LDAP entry or about a set of LDAP entries.

This is done in a strict generic way: the details are accessible through the attribute `data_json`.

Attributes can be ignored by `ignore_attributes` or `ignore_attribute_patterns`.

Attributes can be restricted by `restrict_attributes`.

Attributes can be encoded base64 by `base64encode_attributes` or `base64encode_attribute_patterns`.

Since version v0.4 `dn` can be used as alternative to `ou` and `filter`.

## Resources

The provider makes it possible to provide an LDAP entry. This can be used to create, modify, and delete LDAP entries.

This is done in a strict generic way: the details are specified through the attribute `data_json`.

LDAP attribute are case insensitive according to the LDAP specification.
The [go ldap library](https://pkg.go.dev/github.com/go-ldap/ldap/v3) implements this only for single attributes.
For efficiency reasons this provider uses ldap queries.

The provider can be configured by `entry_attribute_names_case_sensitive` to handle attribute names case sensitive or not.
For efficiency reasons the default is `true`.

If `entry_attribute_names_case_sensitive` is set to `false` in the provider stanza, a list of attributes to be handled case sensitive can be specified by `case_sensitive_attribute_names` in the resource.

## Why generic?

I am convinced that a terraform provider should match the structure of the API used.

The other perspective would be to match the structure of a specific LDAP service. 
The argument against is that LDAP services are generic tools with different characteristics.   

If you need specific structures, you can code them in terraform.
If you like to reuse these structures, you can implement a terraform module.

## Example Usage

```terraform
terraform {
  required_providers {
    ldap = {
      source  = "l-with/ldap"
      version = ">= 0.4"
    }
  }
}

variable "ldap_bind_user" {}
variable "ldap_bind_password" {}

provider "ldap" {
  host = "example.com"
  port = 636
  tls  = true

  bind_user     = var.ldap_bind_user
  bind_password = var.ldap_bind_password
}

data "ldap_entry" "user" {
  ou     = "ou=People,dc=example,dc=com"
  filter = "mail=user@example.com"
}

resource "ldap_entry" "users_example_com" {
  dn = "ou=users,dc=example,dc=com"
  data_json = jsonencode({
    objectClass = ["organizationalUnit"]
  })
}

resource "ldap_entry" "user_jim_mit" {
  dn = "uid=jimmit01,${ldap_entry.users_example_com.dn}"
  data_json = jsonencode({
    objectClass = ["inetOrgPerson"]
    ou          = ["users"]
    givenName   = ["Jim"]
    sn          = ["Mit"]
    cn          = ["Jim Mit"]
  })
}

data "ldap_entry" "user_jim_mit" {
  depends_on = [ldap_entry.user_jim_mit]

  dn = "uid=jimmit01,${ldap_entry.users_example_com.dn}"
}

locals {
  user_data         = jsondecode(data.ldap_entry.user.data_json)
  user_jim_mit_data = jsondecode(data.ldap_entry.user_jim_mit.data_json)
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `bind_password` (String) LDAP password, can optionally be passed as `LDAP_BIND_PASSWORD`environment variable
- `bind_user` (String) LDAP username, can optionally be passed as `LDAP_BIND_USER`environment variable
- `host` (String) LDAP host, can optionally be passed as `LDAP_HOST`environment variable
- `port` (Number) LDAP port, can optionally be passed as `LDAP_PORT`environment variable

### Optional

- `entry_attribute_names_case_sensitive` (Boolean) if the entry attribute names should be handeled case sensitive (for state handling in resource ldap_entry)
- `tls` (Boolean) Enable the TLS encryption for LDAP (LDAPS). Default, is `false`.
- `tls_insecure` (Boolean) Don't verify the server TLS certificate. Default is `false`.